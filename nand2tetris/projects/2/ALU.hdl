// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx
    Mux16(a=x, b=false, sel=zx, out=preSetX1);

    // nx
    Not16(in=preSetX1, out=preSetX2);
    Mux16(a=preSetX1, b=preSetX2, sel=nx, out=preSetX3);

    // zy
    Mux16(a=y, b=false, sel=zy, out=preSetY1);

    // ny
    Not16(in=preSetY1, out=preSetY2);
    Mux16(a=preSetY1, b=preSetY2, sel=ny, out=preSetY3);

    // out = x + y
    Add16(a=preSetX3, b=preSetY3, out=xAddY);

    // out = x & y
    And16(a=preSetX3, b=preSetY3, out=xAndY);

    // f
    Mux16(a=xAndY, b=xAddY, sel=f, out=preliminaryOut);

    // no 
    Not16(in=preliminaryOut, out=preliminaryOut2);
    Mux16(a=preliminaryOut, b=preliminaryOut2, sel=no, out=out, out=out2);

    // zr
    Or16Way(in=out2, out=notZero);
    Not(in=notZero, out=zr);

    // ng
    IsNeg(in=out2, out=ng);
}